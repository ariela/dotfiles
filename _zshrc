## 基本設定 ==============================================================


## 環境変数設定 ==========================================================
# パス設定
typeset -xT SUDO_PATH sudo_path
typeset -U sudo_path
sudo_path=({,/usr/pkg,/usr/local,/usr}/sbin(N-/))

[ ! `echo $PATH | grep "/usr/local/bin"` ] && export PATH=$PATH:/usr/local/bin
[ ! `echo $PATH | grep "${HOME}/bin"` ]    && export PATH=$PATH:${HOME}/bin

# EDITOR設定
export EDITOR=vim

# LANG設定
export LANG=ja_JP.UTF-8

# ターミナル設定
export TERM=xterm-256color

# 実行時間
REPORTTIME=3


#------------------------------------------------------------------------
# log configuration
#------------------------------------------------------------------------
HISTFILE=${HOME}/.zsh_history
HISTSIZE=100000
SAVEHIST=100000
setopt hist_ignore_dups
setopt share_history
#setopt extended_history
#function history-all { history -E 1 }
#setopt COMPLETE_IN_WORD


#------------------------------------------------------------------------
# completion configuration
#------------------------------------------------------------------------
fpath=(${HOME}/.zsh/functions/Completion ${fpath})
fpath=(${HOME}/.zsh/modules/zsh-completions/src ${fpath})

autoload -U compinit; compinit
setopt complete_in_word
setopt glob_complete
setopt hist_expand
setopt numeric_glob_sort
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*:default' memu select=2
zstyle ':completion:*' format '%B%d%b'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z} r:|[._-]=*'
zstyle ':completion:*' use-cache yes
zstyle ':completion:*' verbose yes
#zstyle ':completion:*' completer _oldlist \
#                                 _match \
#                                 _history \
#                                 _ignored \
#                                 _approximate \
#                                 _prefix
zstyle ':completion:*:sudo:*' command-path /usr/local/sbin \
                                           /usr/local/bin \
                                           /usr/sbin \
                                           /usr/bin \
                                           /sbin \
                                           /bin \
                                           /usr/X11R6/bin


#------------------------------------------------------------------------
# prompt configuration
#------------------------------------------------------------------------
autoload -U colors zsh/terminfo
colors
setopt prompt_subst

source ${HOME}/.zsh/256colorlib.sh
local HOSTC=$'%{\e[48;5;'"$(printf "%d\n" 0x$(hostname|md5sum|cut -c1-2))"'m%}'
local DEFAULTC=$'%{\e[m%}'

case "${OSTYPE}" in
darwin*)
    local ETH0IPADDR=`ifconfig en0 | grep "inet " | grep "broadcast" | sed "s/.*inet \([0-9]*\.[0-9]*\.[0-9]*\.[0-9]*\).*/\1/"`
    ;;
linux*)
    local ETH0IPADDR=`ip addr show eth0 | grep "inet " | head -1 | sed "s/.*inet \([0-9]*\.[0-9]*\.[0-9]*\.[0-9]*\).*/\1/"`
    ;;
esac

local p_dir="\
${HOSTC} ${ETH0IPADDR} ${DEFAULTC}\
%K{019}%F{069} %n %f%k\
%K{136}%F{220} @ %f%k\
%K{161}%F{212} %m %f%k\
%K{023}%F{082} %~ %f%k\
"
local p_mark="%(?,%F{83},%F{162})%(!,#,%%)%f"

local pl_line1="${p_dir}"
local pl_line2="${p_mark}"

PROMPT="${pl_line1}"$'\n'"${pl_line2} "

#------------------------------------------------------------------------
# version controll configuration
#------------------------------------------------------------------------
RPROMPT=""

autoload -Uz vcs_info
autoload -Uz add-zsh-hook
autoload -Uz is-at-least
autoload -Uz colors

# 以下の3つのメッセージをエクスポートする
#   $vcs_info_msg_0_ : 通常メッセージ用 (緑)
#   $vcs_info_msg_1_ : 警告メッセージ用 (黄色)
#   $vcs_info_msg_2_ : エラーメッセージ用 (赤)
zstyle ':vcs_info:*' max-exports 3

zstyle ':vcs_info:*' enable git svn hg bzr
# 標準のフォーマット(git 以外で使用)
# misc(%m) は通常は空文字列に置き換えられる
zstyle ':vcs_info:*' formats '(%s)-[%b]'
zstyle ':vcs_info:*' actionformats '(%s)-[%b]' '%m' '<!%a>'
zstyle ':vcs_info:(svn|bzr):*' branchformat '%b:r%r'
zstyle ':vcs_info:bzr:*' use-simple true


if is-at-least 4.3.10; then
    # git 用のフォーマット
    # git のときはステージしているかどうかを表示
    zstyle ':vcs_info:git:*' formats '(%s)-[%b]' '%c%u %m'
    zstyle ':vcs_info:git:*' actionformats '(%s)-[%b]' '%c%u %m' '<!%a>'
    zstyle ':vcs_info:git:*' check-for-changes true
    zstyle ':vcs_info:git:*' stagedstr "+"    # %c で表示する文字列
    zstyle ':vcs_info:git:*' unstagedstr "-"  # %u で表示する文字列
fi

# hooks 設定
if is-at-least 4.3.11; then
    # git のときはフック関数を設定する

    # formats '(%s)-[%b]' '%c%u %m' , actionformats '(%s)-[%b]' '%c%u %m' '<!%a>'
    # のメッセージを設定する直前のフック関数
    # 今回の設定の場合はformat の時は2つ, actionformats の時は3つメッセージがあるので
    # 各関数が最大3回呼び出される。
    zstyle ':vcs_info:git+set-message:*' hooks \
                                            git-hook-begin \
                                            git-untracked \
                                            git-push-status \
                                            git-nomerge-branch \
                                            git-stash-count

    # フックの最初の関数
    # git の作業コピーのあるディレクトリのみフック関数を呼び出すようにする
    # (.git ディレクトリ内にいるときは呼び出さない)
    # .git ディレクトリ内では git status --porcelain などがエラーになるため
    function +vi-git-hook-begin() {
        if [[ $(command git rev-parse --is-inside-work-tree 2> /dev/null) != 'true' ]]; then
            # 0以外を返すとそれ以降のフック関数は呼び出されない
            return 1
        fi

        return 0
    }

    # untracked フィアル表示
    #
    # untracked ファイル(バージョン管理されていないファイル)がある場合は
    # unstaged (%u) に ? を表示
    function +vi-git-untracked() {
        # zstyle formats, actionformats の2番目のメッセージのみ対象にする
        if [[ "$1" != "1" ]]; then
            return 0
        fi

        if command git status --porcelain 2> /dev/null \
            | awk '{print $1}' \
            | command grep -F '??' > /dev/null 2>&1 ; then

            # unstaged (%u) に追加
            hook_com[unstaged]+='?'
        fi
    }

    # push していないコミットの件数表示
    #
    # リモートリポジトリに push していないコミットの件数を
    # pN という形式で misc (%m) に表示する
    function +vi-git-push-status() {
        # zstyle formats, actionformats の2番目のメッセージのみ対象にする
        if [[ "$1" != "1" ]]; then
            return 0
        fi

        if [[ "${hook_com[branch]}" != "master" ]]; then
            # master ブランチでない場合は何もしない
            return 0
        fi

        # push していないコミット数を取得する
        local ahead
        ahead=$(command git rev-list origin/master..master 2>/dev/null \
            | wc -l \
            | tr -d ' ')

        if [[ "$ahead" -gt 0 ]]; then
            # misc (%m) に追加
            hook_com[misc]+="(p${ahead})"
        fi
    }

    # マージしていない件数表示
    #
    # master 以外のブランチにいる場合に、
    # 現在のブランチ上でまだ master にマージしていないコミットの件数を
    # (mN) という形式で misc (%m) に表示
    function +vi-git-nomerge-branch() {
        # zstyle formats, actionformats の2番目のメッセージのみ対象にする
        if [[ "$1" != "1" ]]; then
            return 0
        fi

        if [[ "${hook_com[branch]}" == "master" ]]; then
            # master ブランチの場合は何もしない
            return 0
        fi

        local nomerged
        nomerged=$(command git rev-list master..${hook_com[branch]} 2>/dev/null | wc -l | tr -d ' ')

        if [[ "$nomerged" -gt 0 ]] ; then
            # misc (%m) に追加
            hook_com[misc]+="(m${nomerged})"
        fi
    }


    # stash 件数表示
    #
    # stash している場合は :SN という形式で misc (%m) に表示
    function +vi-git-stash-count() {
        # zstyle formats, actionformats の2番目のメッセージのみ対象にする
        if [[ "$1" != "1" ]]; then
            return 0
        fi

        local stash
        stash=$(command git stash list 2>/dev/null | wc -l | tr -d ' ')
        if [[ "${stash}" -gt 0 ]]; then
            # misc (%m) に追加
            hook_com[misc]+=":S${stash}"
        fi
    }

fi

function _update_vcs_info_msg() {
    local -a messages
    local prompt

    LANG=en_US.UTF-8 vcs_info

    if [[ -z ${vcs_info_msg_0_} ]]; then
        # vcs_info で何も取得していない場合はプロンプトを表示しない
        prompt=""
    else
        # vcs_info で情報を取得した場合
        # $vcs_info_msg_0_ , $vcs_info_msg_1_ , $vcs_info_msg_2_ を
        # それぞれ緑、黄色、赤で表示する
        [[ -n "$vcs_info_msg_0_" ]] && messages+=( "%F{green}${vcs_info_msg_0_}%f" )
        [[ -n "$vcs_info_msg_1_" ]] && messages+=( "%F{yellow}${vcs_info_msg_1_}%f" )
        [[ -n "$vcs_info_msg_2_" ]] && messages+=( "%F{red}${vcs_info_msg_2_}%f" )

        # 間にスペースを入れて連結する
        prompt="${(j: :)messages}"
    fi

    RPROMPT="$prompt"
}
add-zsh-hook precmd _update_vcs_info_msg



#------------------------------------------------------------------------
# nvm configuration
#------------------------------------------------------------------------
if [ -f ${HOME}/.nvm/nvm.sh ]; then
    . ${HOME}/.nvm/nvm.sh
    nvm use default > /dev/null
    npm_dir=${NVM_PATH}_modules
    export NODE_PATH=${npm_dir}
fi


#------------------------------------------------------------------------
# rvm configuration
#------------------------------------------------------------------------
if [ -f ${HOME}/.rvm/installed.at ]; then
    # Add RVM to PATH for scripting

    [ ! `echo $PATH | grep "${HOME}/.rvm/bin"` ]    && export PATH=${HOME}/.rvm/bin:$PATH
fi


#------------------------------------------------------------------------
# Alias configuration
#------------------------------------------------------------------------
setopt complete_aliases

case "${OSTYPE}" in
freebsd*|darwin*)
    alias ls="ls -G -w"
    alias ll='ls -G -w -al'
    alias la='ls -G -w -a'
    ;;
linux*)
    alias ls="ls --color=auto -v"
    alias ll='ls --color=auto -alh'
    alias la='ls --color=auto -a'
    ;;
esac

alias where="command -v"
alias j="jobs -l"
alias grep='grep --color=auto'
alias rm='rm -i'
alias cp='cp -i'
alias mv='mv -i'
alias cls='clear'
alias lf="ls -F"
alias du="du -h"
alias df="df -h"
alias vi="vim"
alias sudo="sudo PATH=$PATH"


#------------------------------------------------------------------------
# Load environment configuration
#------------------------------------------------------------------------
case "${OSTYPE}" in
darwin*)
    [ -f ~/.zshrc.osx ] && source ~/.zshrc.osx
    ;;
linux*)
    [ -f ~/.zshrc.linux ] && source ~/.zshrc.linux 
    ;;
esac


#------------------------------------------------------------------------
# Load local configuration
#------------------------------------------------------------------------
[ -f ~/.zshrc.local ] && source ~/.zshrc.local
